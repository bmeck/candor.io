print = global.print
p = global.prettyPrint
require = global.require

throw = (err) {
  p(err)
  global.exit()
}

// Used to emit on errors when libuv badness happens
check = (status) {
  if (status) {
    throw(require('uv').lastError())
  }
}

Tcp = require('tcp')

p("Tcp", Tcp)
server = Tcp.create()
check(Tcp.bind(server, "0.0.0.0", 8080))

onConnection = (client) {
  p("onConnection")

  check(Tcp.readStart(client, (nread, chunk) {
    if (nread == -1) {
      err = global.uv.lastError()
      if (err.name == "EOF") {
        p("EOF")
        Tcp.shutdown(client, () {
          p("shutdown")
          Tcp.close(client, () {
            p("closed")
          })  
        })
      } else {
        throw(err)
      }
      return
    }
    if (nread > 0) {
      p("Echoing", chunk)
      Tcp.write(client, chunk)
      return
    }
  }))

}

check(Tcp.listen(server, 128, () {
  client = Tcp.create()
  check(Tcp.accept(server, client))
  onConnection(client)
}))
print("TCP echo server listening on port 8080")